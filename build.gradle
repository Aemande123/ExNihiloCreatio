
// For those who want the bleeding edge
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()

        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }

        maven {
            name = "gradle"
            url "https://plugins.gradle.org/m2/"
        }

        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "maven"

version = "1.12-0.1.3" + (System.getenv("BUILD_NUMBER") != null ? "." + System.getenv("BUILD_NUMBER") : "")
def mod_version = version
group= "exnihilocreatio" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "exnihilocreatio"

sourceCompatibility=1.8
targetCompatibility=1.8

minecraft {
    version = "1.12.1-14.22.0.2474"
    runDir = "run"
	mappings = "snapshot_20170810"
    useDepAts = true

    replace "@VERSION@", "$mod_version"
    replaceIn "ExNihiloCreatio.java"
}

repositories
{
    maven { 
    	// Required for JEI and Mantle
    	name "DVS1 Maven FS" 
    	url "http://dvs1.progwml6.com/files/maven" 
    }

    maven {
        // Required for HWYLA
        name "tehnut"
        url "http://tehnut.info/maven"
    }

    maven {
        // Required for TheOneProbe
        name "tterrag"
        url "http://maven.tterrag.com"
    }
	
	maven {
	    // Required for RedstoneFlux which is required for TheOneProbe
        name = "CoFH Maven"
        url = "http://maven.covers1624.net"
    }
}

dependencies
{
    compileOnly "mezz.jei:jei_1.12:4.7.+:api"
    // runtime "mezz.jei:jei_1.12:4.7.+"
    
    deobfCompile "slimeknights.mantle:Mantle:1.12-1.3.+"
    deobfCompile "slimeknights:TConstruct:1.12-2.7.+"
    
    // TODO: Add back EnderIO support once it has updated [To fix AE2 Dependency Error]
    // deobfCompile "com.enderio.core:EnderCore:1.10.2-0.4.1.65-beta"
    // deobfCompile "com.enderio:EnderIO:1.10.2-3.1.+"

    compileOnly "mcp.mobius.waila:Hwyla:1.8.20-B35_1.12:api"
    runtime "mcp.mobius.waila:Hwyla:1.8.20-B35_1.12"

	compileOnly "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.+:api"
    runtime "mcjty.theoneprobe:TheOneProbe-1.12:1.12-1.4.+"

    compileOnly "org.projectlombok:lombok:1.16.10"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
                
        // replace version and mcversion
        expand "version":project.version, "mcversion":project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    from(sourceSets.api.output)

    manifest {
        attributes 'FMLAT': 'exnihilocreatio_at.cfg'
    }

}


// Create deobf dev jars
task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + System.getenv("local_maven"))
        }
    }
}
